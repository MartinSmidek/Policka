#pragma test
# Systém FiS - modul Klub.Dary
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

# ================================================================================================== dry - Dary
const klu_dry_dar = 310
const klu_dry_dary = 11

use C_dary: form _dary [5,0,,] { tabindex:20 }
use D_calc: form _calc [580,0,,]

proc onstart () {
  C_dary.cond.key(500);
  C_dary._dary2_vyber;
  C_dary._dary2_focus("<h1>Všechny dary</h1>",1);
  D_calc.display(0);
  C_dar.kas.stisk(1); C_dar.kas.set_css('','changed');
  C_dar._dar_start;
}

# aktivace_zvenčí
proc show_dar (x) {
  [ C_dary.dary.id_dar.set_query(1,x,0) ];
  [ C_dary.dary.id_dar.set_query(2,x,1) ];
  panel.focus;
  C_dary.dary.browse_focus;
  C_dary.dary.browse_select("left(dd.deleted,1)!='D'");
  D_calc.display(1)
}
proc show_balicek (x,kolik) {
  C_dary.dary.browse_init;
  C_dar._dar_init;
  C_clen.init;
  C_dary._dary2_focus(conc("balíček <b>",x,"</b>"),0);
  [ C_dary.dary.ucet.set_query(1,x) ];
  panel.focus;
  C_dary.dary.browse_focus; D_calc.celkem.set(kolik);
  C_dary.dary.browse_select("left(dd.deleted,1)!='D'");
  D_calc.display(1)
}
# kalkulace
proc D_kalkulace () { D_calc.soucet.set(C_dary.dary.browse_map('klub_dary_soucet')) }

# -------------------------------------------------------------------------------------------------- _ukoly
use C_ukoly: form klu._ukoly [580,klu_cle_dary+19,,] {
  proc u_show() { C_ukoly.get.display(2,'u') }
}
use C_zpravy: form klu._zpravy [580,klu_cle_dary+19,,] {
  proc c_show() { C_zpravy.get.display(2,'x') }
}
# -------------------------------------------------------------------------------------------------- _clen
use C_clen: form klu._clen [580,klu_dry_dar,,] { tabindex:60 }
# -------------------------------------------------------------------------------------------------- _dary
form _dary [,,560,klu_dry_dar] {
  # proměnné
  var _dary2_cond: text,
  menu { type:'context'
    item sel_off { title:'zrušit výběr' proc onclick () {
      dary.selected('clear'); _dary2_load; D_kalkulace } },
  },
  # výběr_darů
  label [2,2,568,30] { css:'ae_parm'},
  label zobrazeno [9,7,117,15] { title:'<h1>Všechny dary</h1>' },
  select cond [124,9,105,17] { type:'map', map_pipe:cis_podminky2.hodnota,
    options:cis_podminky2.zkratka, format:'t'
    proc onchange () { _dary2_vyber } }
  field cond_dne [237,9,85,18] { format:'xt', type:'date', sql_pipe:'sql_date1'
    proc onchange () { _dary2_vyber } }
  select cond_kdo [329,9,81,17] { type:'map', options:user2.surname, format:'xt'
    proc onchange () { _dary2_vyber } }
  proc _dary2_vyber () {
    _dary2_cond.set(replace(cond.get,'$user',cond_kdo.key,'$datum',date2sql(cond_dne.get)));
    _dary2_load
  }
  # přepínání sloupců
  check show_del [421,0,78,18] { title:'i smazané', format:'t', value:'0'
    proc onchange () {
      {  show_del.get; dary.del.width(10); dary.castka.width(65)
      | dary.del.width(0); dary.castka.width(76) };
      _dary2_load
  } },
  check show_new [421,14,70,18] { title:'jen nové', format:'t', value:'1'
    proc onchange () { _dary2_load } }
  // check show_our [500,0,72,17] { title:'jen naše', format:'t', help:'jen naše',  -- potlačeno 13/8/2013
  //  value:'0', skill:'hd|hdks'
  //  proc onchange () { _dary2_load } },
  # data
  view dd: table dar,
  view c: table clen {join_type:'LEFT', join:'ON dd.id_clen=c.id_clen'},
  browse dary [4,34,0,54] {rows:klu_dry_dary, qry_rows:2, wheel:5, //css_rows:'druh,1:dar_1,2:dar_2'
    show del    [,, 0,] { title:'D',         data:dd.deleted, format:'q$' },
    show id_dar [,,55,] { title:'dar č.',    data:dd.id_dar, format:'q=rfs' },
    show        [,,70,] { title:'ze dne',    data:dd.castka_kdy, format:'rfs-' },
    show castka [,,76,] { title:'částka',    data:dd.castka, format:'q/rfs' },
    show zpusob [,,12,] { title:'z',         data:dd.zpusob, map_pipe:cis_k_zpusob.zkratka },
    show stred  [,,20,] { title:'s',         data:dd.stredisko, map_pipe:cis_stredisko.zkratka },
    show ucet   [,,70,] { title:'účet/bal.', data:dd.ucet, format:'q$fs' },
    show        [,,124,] { title:'popis',    data:dd.popis, format:'q$fs' },
    show        [,,124,] { title:'pozn.',    data:dd.pozn, format:'q$fs' },
    show id_clen [,,40,] { title:'člen č.',  data:dd.id_clen, format:'q=rfs' },
    show        [,,80,] { title:'firma',     data:c.firma, format:'q$fs' },
    show        [,,80,] { title:'příjmení',  data:c.prijmeni, format:'q$fs' },
    show        [,,70,] { title:'jméno',     data:c.jmeno, format:'q$fs' },
//     show        [,,80,] { title:'ulice',     data:c.ulice, format:'q$s' },
//     show        [,,48,] { title:'psč',       data:c.psc, format:'q$s', sql_pipe:'psc' },
    show        [,,80,] { title:'obec',      data:c.obec, format:'q$fs' },
    show        [,,25,] { title:'stát',      data:c.stat, format:'q$fs' },
    show kdo { data:dd.zmena_kdo, map_pipe:user.abbr },
    show kdn { data:dd.zmena_kdo },
    show kdy { data:dd.zmena_kdy },
    proc onchange () {
      this.browse_count; onrowclick
    | C_clen.init; C_dar._dar_init }
    proc onrowclick () {
      { id_clen.get; C_clen.load(id_clen.get); C_ukoly._ukoly_load(id_clen.get)
      | C_clen.init;
        warning('POZOR: dar č.',id_dar.get,
          ' nemá dárce - lze jej přeřadit správnému dárci tlačítkem [Přepsat] nebo jej lze [Smazat]');
      };
      C_dar._dar_load(id_dar.get);
      zmena.set(conc('<small>poslední změna: ',kdo.get,' ',kdy.get,'</small>'));
      { id_clen.get | C_dar.kopiruj_dar.enable(0); C_dar.vytvor_dar.enable(0) };
      dary.focus; dary.browse_focus
    }
    // DblClk přejde na dárce
    proc onsubmit () { klu.cle.show_clen(C_dary.dary.id_clen.get) }
    proc onchoice () { D_kalkulace;   }
  },
  label zmena [12,-2,391,18] { title:'' },
  # procedury
  proc _dary2_focus (titl,reload) {
    dary.browse_focus; dary.init_queries(reload); zobrazeno.set(titl) }
  proc _dary2_load () {
    C_dar._dar_init;
    dary.browse_load(conc(_dary2_cond.get,
      cconc(
        not(C_dary.show_del.get)," AND left(dd.deleted,1)!='D'",
        // C_dary.show_our.get,conc(" AND FIND_IN_SET(dd.stredisko,'",klu.cond_strediska.get,"')"), -- potlačeno 13/8/2013
        C_dary.show_new.get," AND year(castka_kdy)>=year(curdate())-1"
    )));
    dary.raise('onrowclick');
  }
}
# -------------------------------------------------------------------------------------------------- _dar
use C_dar: form klu._dar [10,klu_dry_dar,,] {   // doplnění funkcionality metod klu._dar
  # Nový
  proc vytvor_dar.onclick() {
    form.desc._dar_novy(C_dary.dary.id_clen.get); }
  # Uložit
  proc _dar_uloz () {
    var stav: number
    stav.set(form.desc._dar_uloz);
    echo('stav=',stav);
    //_dar_uloz: 0 - beze změny, 1 - uložení změny, 2 - vytvoření nového
    // s případnou úpravou příslušnosti ke středisku
    switch(stav,
     0,{ stop },
     1,{ C_dary.dary.browse_row;
         [ ask('klub_clen_stredisko',C_dar.id_clen.get); C_clen.load ]
       },
     2,{ C_dary.dary.browse_seek(conc('id_dar=',C_dar.key));
         [ ask('klub_clen_stredisko',C_dar.id_clen.get); C_clen.load ]
       | warning("dar byl vytvořen ale není zobrazený v levém seznamu (nevyhovuje nastaveným podmínkám)")
       }
    )
  }
  proc _dar_zpet (iddar) { echo(3);
    form.desc._dar_zpet(C_dary.dary.browse_key); }
}
# -------------------------------------------------------------------------------------------------- _calc
form _calc [,,400,40] {
  label [2,2,391,30] { css:'ae_parm'},
  field soucet [157,8,78,18] { value:'0', format:'r', help:'součet|součet označených darů' },
  field celkem [309,8,78,18] { value:'0', format:'r', help:'celkem|částka od České pošty' },
  label [9,7,50,15] { title:'<h1>Kalkulačka</h1>' },
  label [100,11,50,17] { title:'označené' },
  label [245,11,50,17] { title:'poukázané' },
}
