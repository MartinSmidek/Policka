# Systém CK - panel Klub.Kontakty
# (c) 2010 Martin Šmídek <martin@smidek.eu>

# ================================================================================================== KONTAKTY
# const klu_cle_cleni = 13
# const klu_cle_dary = 334

use C_cleni: form _cleni [5,0,,] { tabindex:20 }
use C_clen_butt: form _clen_butt [583,klu_cle_dary-57,,] { tabindex:30 }
use C_dary: form _dary [5,klu_cle_dary,,] { tabindex:50, skill:'hdd' }

proc onstart () {
  chap.modul_state.set(°{active:1,dirty:0},'klu.cle');   # poznačení, že modul je aktivní
  C_cleni.cond.key(1015);       // s kontaktem = 1015
  C_cleni._cleni_vyber(0);
  C_clen.kon.onclick;
  Ctrack.clen_form.set(C_clen);
  has_skill('hdd');
  C_dar._dar_start;
  C_dar.kas.stisk(1); C_dar.kas.set_css('','changed');
}
# proc ukaz_cleny() {
#   C_cleni.cond.key(1015);
#   C_cleni.cond_sel.set(1);
#   C_cleni.cleni.selected('use');
#   C_cleni._cleni_vyber(0);
#   C_cleni.cleni.browse_focus;
# }

# aktivace_zvenčí
proc select_cleny(ids) {
  panel.focus;
  C_cleni.cleni.selected('set',ids);
  C_cleni.cleni.raise('onrowclick')
}
proc show_clen (id) {
  [ C_cleni.cleni.init_queries(0) ];
  [ C_cleni.cleni.id_clen.set_query(1,id,1) ];
  panel.focus;
  C_cleni.cleni.raise('onrowclick')
}
proc curr_clen(mode) { // 1:id, 2:jméno
  return(cconc(
    eq(mode,1),C_clen.key,
    eq(mode,2),conc(C_clen.jmeno.get,' ',C_clen.prijmeni.get,"'")));
}

proc onfocus () {
  chap.modul_state.get;           /* !!!!!!!!!!!!!!!!!!!!!!!!!! když onfocus proběhne před onstart */
  chap.modul_state.set('cle','klu.last');   # poznačení aktivnosti cle
  C_cleni._cleni_focus
}
# ------------------------------------------------------------------------------------------- _cleni
form _cleni [,,565,klu_cle_dary] {
  # proměnné
  var _cleni_cond: text,
  # výběr_členů
  label [2,2,570,30] { css:'ae_parm'},
  label [9,6,50,15] { title:'<h1>Dárci</h1>'},
  select cond [64,9,170,] { type:'map', map_pipe:cis_vyber.hodnota,
    options:cis_vyber.zkratka, format:'t', help:'výběr|základní výběr definovaný v číselníku'
    proc onchange () { _cleni_vyber(0) } }
  field cond_dne [240,9,87,18] { format:'xt', type:'date', sql_pipe:'sql_date1',
    help:'kdy|uplatní se jen ve výběrech s kdy:...'
    proc onchange () { _cleni_vyber(0) } }
  select cond_kdo [333,9,83,] { type:'map', options:user2.surname, format:'xt',
    help:'kdo|uplatní se jen ve výběrech s kdo:...'
    proc onchange () { _cleni_vyber(0) } }
  check cond_sel [500,0,70,17] { title:'vybraní', style:'color:#d30', format:"t",
    help:'výběr pomocí Insert', value:'0'
    proc onchange () {
      cleni.selected(cconc(this.get,'use',not(this.get),'ignore')); _cleni_vyber(0) } }
    #check cond_our [500,14,86,17] { title:'jen naši', format:"t",
    #help:'budou zobrazeni jen dárci našeho střediska', value:'0'
    #proc onchange () { _cleni_vyber(0) } }
  check cond_xel [423,14,72,17] { title:'i zemřelí', format:"t",
    help:'budou zobrazeni i zemřelí', value:'0'
    proc onchange () { _cleni_vyber(0) } }
  var _cond: text
  proc _cleni_vyber (clenid) {
    _cond.set(replace(cond.get,'$user',cond_kdo.key,'$datum',date2sql(cond_dne.get)));
    { _cond.get | _cond.set(1) };
    _cond.set(cconc(1,_cond.get,
      not(cond_del.get)," AND left(c.deleted,1)!='D'",
      #cond_our.get,conc(" AND FIND_IN_SET(c.stredisko,'",klu.cond_strediska.get,"')"),
      not(cond_xel.get)," AND umrti='0000-00-00'"));
    C_clen.init; //C_dary.dary.browse_init; C_dar.init;
    { clenid; echo('seek',clenid); cleni.browse_seek
    | cleni.browse_load(_cond.get);
      /*cleni.raise('onrowclick')*/
    | C_dary.dary.browse_init; C_dar._form_state('','b.')
    } }
  # přepínání_sloupce_del
  check cond_del [423,0,86,17] { title:'i smazaní', format:"t",
    help:'budou zobrazeni i smazaní', value:'0'
    proc onchange () {
      { this.get; cleni.del.width(10); cleni.firma.width(59)
      | cleni.del.width(0); cleni.firma.width(70) };
      _cleni_vyber(0) }}
  # kontextové menu
  menu { type:'context'
    item sel_off { title:'zrušit výběr'
      proc onclick () { cleni.selected('clear'); _cleni_vyber(0) } }
#     item raynet { title:'přejít na RAYNET',                                             skill:'a'
#       proc onclick () { var id:number, ret:object
#         id.set(cleni.raynet.get); id;
#         ret.set(ask('reynet_get','person',id));
#         alert("v RAYNET je uvedeno: ",ret.firstName,' ',ret.lastName,',',ret.primaryRelationship.company.name)
#       | warning("propojení na RAYNET nebylo ještě definováno pro ",cleni.id_clen.get)
#       } }
  }
  # data
  view c: table clen,
  browse cleni [4,34,0,0] {type:'smart', buf_rows:21, rows:klu_cle_cleni, qry_rows:1, wheel:5
    show del [,,0,] { title:'D', data:c.deleted, format:'q=' }
    show id_clen  [,, 43,] { title:'dárce', data:c.id_clen, format:'rq=s' }
    show firma    [,, 70,] { title:'firma', data:c.firma, format:'tq$s' }
    show prijmeni [,, 75,] { title:'příjmení', data:c.prijmeni, format:'tq$s+' }
    show jmeno    [,, 55,] { title:'jméno', data:c.jmeno, format:'tq$s' }
    show ulice    [,, 87,] { title:'ulice', data:c.ulice, format:'tq$s' }
    show psc      [,, 42,] { title:'psč', data:c.psc, format:'q$s', sql_pipe:'psc' }
    show obec     [,, 81,] { title:'obec', data:c.obec, format:'tq$s' }
    // výběrové položky
    show ucet     [,,80,] { title:'účet',      data:c.ucet,       format:'q$rs' }
    show rodcis   [,, 0,] { title:'rč/IČ',     data:c.rodcis,     format:'q$s' }
    show titul    [,, 0,] { title:'titul',     data:c.titul,      format:'q$s' }
    show telefony [,, 0,] { title:'telefony',  data:c.telefony,   format:'q$s' }
    show email    [,, 0,] { title:'email',     data:c.email,      format:'q$s' }
    show potvrz   [,, 0,] { title:'potvrzení', data:c.potvrzeni,  format:'q#s',
      map_pipe:cis_k_potvrzeni.zkratka }
    show jenvan   [,, 0,] { title:'jen vánoční',data:c.jenvanocni,format:'q=s:e' }
#     show raynet   [,, 0,] { title:'RAYNET',     data:c.ID_RAYNET, format:'q*rs:e' }
#     show old_kod  [,, 0,] { title:'(kód)',     data:c.kod,       format:'q=rs' }
    show stredisko[,, 0,] { title:'středisko', data:c.stredisko,
      map_pipe:cis_darce.hodnota, format:'q#s' },
    show kategorie[,, 0,] { title:'kategorie', data:c.kategorie, format:'q#s'
      map_pipe:cis_kategorie.zkratka}
    proc onclick() {
      jenvan.width;    jenvan.width(0);    stredisko.width(80); // raynet.width(80);
#     | raynet.width;    raynet.width(0);    stredisko.width(80);
    | stredisko.width; stredisko.width(0); ucet.width(80);
    | ucet.width;      ucet.width(0);      kategorie.width(80);
    | kategorie.width; kategorie.width(0); rodcis.width(80);
    | rodcis.width;    rodcis.width(0);    titul.width(80);
    | titul.width;     titul.width(0);     telefony.width(80);
    | telefony.width;  telefony.width(0);  email.width(80);
    | email.width;     email.width(0);     potvrz.width(80);
    | potvrz.width;    potvrz.width(0);    jenvan.width(80);
    }
    # výběr_posledního_sloupce
    show kdo { data:c.zmena_kdo },
    show kdn { data:c.zmena_kdo, map_pipe:user.abbr },
    show kdy { data:c.zmena_kdy },
    # události
    proc onrowclick() {
      znacka;
    }
    proc onchange() {
      this.browse_count; znacka
    | C_clen.init; C_clen_butt._form_state('be','bc|br');
      C_dary.dary.browse_init;
      C_dar._dar_init; C_dar._form_state('','b.'); C_dary.suma.set(''); }
    proc znacka () {
      cleni.browse_count;
      C_clen_butt._clen_load(id_clen.get);
      zmena.set(conc('<small>poslední změna: ',kdn.get,' ',kdy.get,'</small>'));
      [ has_skill('hdd'); C_dary._dary_load ];
      C_ukoly._ukoly_load(id_clen.get);
      C_zpravy._zpravy_load(id_clen.get)
    | C_clen.init; zmena.set('');
      has_skill('hdd'); C_dary.zmena.set(''); C_dary.suma.set(''); C_dary.dary.browse_init; C_dar.init
    }
  },
  label zmena [12,-2,391,18] { title:'' },
  # procedury
  proc _cleni_focus () {
    cleni.browse_focus }
  proc _cleni_load () {
    cleni.browse_load(_cleni_cond.get); /*cleni.raise('onrowclick')*/  }
}
# -------------------------------------------------------------------------------------------- _clen
use C_ukoly: form klu._ukoly [583,24,,] { tabindex:30
  proc u_show() { C_ukoly.get.display(1,'u|b') }
}
use C_zpravy: form klu._zpravy [583,24,,] { tabindex:30
  proc c_show() { C_zpravy.get.display(1,'x|y|b') }
}
use C_clen: form klu._clen [583,4,,] { tabindex:30
  proc _onchanged () { C_clen_butt._form_state('bc','be|br') }
}
# --------------------------------------------------------------------------------------- _clen_butt
form _clen_butt [,,400,50] {
  label    [0,0,399,39] { css:'ae_work' }
  # --------------------------- události
  proc onsubmit () { _clen_uloz(0) }
  # --------------------------- tlačítka a jejich obsluha
  proc _form_state(on,off) {
    form.enable(1,on); form.enable(0,off);
    has_skill('hdcd');
    { C_clen.id.get;
      { eq(substr(C_clen.del.get,0,1),'D');
        zrus_clena.display(0); obnov_clena.display(1)
      | zrus_clena.display(1); obnov_clena.display(0) }
    | zrus_clena.display(0); obnov_clena.display(0) }  }
  # Uložit
  button uloz_clena [9,14,,] { tag:'bc', type:'submit', title:'Uložit ', skill:'hdce|hdce'
    help:'provede uložení změněných (červeně orámovaných) položek'
    proc onclick() { _clen_uloz(0) }  }
  proc _clen_uloz (bez) {
    // bez => bez kontroly integrity (např. při mazání)
    var osl: object
    var integrity: object
    form.enable(1,'br'); form.enable(0,'be|bc');
    C_clen.same; warning('nebyla provedena žádná změna'); C_cleni._cleni_focus
  | { bez
    | integrity.set(ask('klub_check',C_clen.id.get,C_clen.rodcis.get,C_clen.darce.get));
      integrity.ok.get };
    C_clen.kdo.set(sys('user','id_user')); C_clen.kdo.change;
    C_clen.kdy.set(now_sql(1)); C_clen.kdy.change;
    { C_clen.same
    | C_clen.key;
      // je-li potřeba, oprav oslovení
      [ not(eq(C_clen.vyjimka.key,1));
        or(C_clen.osoba.changed,C_clen.titul.changed,C_clen.jmeno.changed,C_clen.prijmeni.changed);
        osl.set(ask('osl_insert',C_clen.osoba.key,
          C_clen.titul.get,C_clen.jmeno.get,C_clen.prijmeni.get));
        C_clen.rod.key(osl.rod.get); C_clen.rod.change(1);
        C_clen.osloveni.key(osl.osloveni.get); C_clen.osloveni.change(1);
        C_clen.prijmeni5p.set(osl.prijmeni5p.get); C_clen.prijmeni5p.change(1);
      ];
      // pokud nebylo opraveno středisko a byla nastavena výjimka na auto, oprav dárce (i když je)
      [ C_clen.svyjimka.changed; not(C_clen.svyjimka.key); not(C_clen.stredisko.changed);
        C_clen.stredisko.key(ask('klub_clen_stredisko',C_clen.key,1));
        C_clen.stredisko.change ];
      // bylo-li opraveno středisko, nastav výjimku
      [ and(C_clen.stredisko.changed,not(C_clen.svyjimka.changed));
        C_clen.svyjimka.key(1); C_clen.svyjimka.change ];
      C_clen.save; C_clen.load; C_cleni.cleni.browse_seek; C_cleni.cleni.znacka
    | // pokud není nastavena výjimka v oslovení, vytvoř je
      [ not(eq(C_clen.vyjimka.key,1));
        osl.set(ask('osl_insert',C_clen.osoba.key,
          C_clen.titul.get,C_clen.jmeno.get,C_clen.prijmeni.get));
        C_clen.rod.key(osl.rod.get); C_clen.rod.change(1);
        C_clen.osloveni.key(osl.osloveni.get); C_clen.osloveni.change(1);
        C_clen.prijmeni5p.set(osl.prijmeni5p.get); C_clen.prijmeni5p.change(1);
      ];
      C_clen.insert;
      C_clen.load;
      { C_cleni.cleni.browse_seek(conc('id_clen=',C_clen.key));
        C_cleni.cleni.znacka
      | bez
      | warning("kontakt byl vytvořen ale za podmínky '",C_cleni.cond.get(1),"' nelze zobrazit")
      };
    | warning('POZOR: chyba při ukládání člena')
    };
    C_cleni.cleni.browse_focus
  | warning(integrity.msg.get)
  }
  # Zpět
  button zpet_clena [65,14,,] { tag:'bc', title:'Zpět', skill:'hdce|hdce'
    help:'zruší červené orámování a vrátí původní obsah'
    proc onclick () {
      C_cleni.cleni.browse_count; C_cleni.cleni.browse_focus; _clen_load(C_cleni.cleni.id_clen.get)
    | C_clen.init; _form_state('be','bc|br') }
  }
  # Nový
  button vytvor_clena [121,14,,] { tag:'be', title:'Nový kontakt', skill:'hdce|hdce'
    help:'naplní položky základními hodnotami, vlastní vytvoření se provede uložením změn'
    proc onclick () { _clen_novy } }
  proc _clen_novy () {
    C_clen.init(1);
    C_clen.osoba.change; C_clen.jmeno.change; C_clen.prijmeni.change;
    C_clen.ulice.change; C_clen.psc.change; C_clen.obec.change; C_clen.historie.change;
    [ moje_stredisko.get; C_clen.stredisko.key(moje_stredisko.get); C_clen.stredisko.change; ];
    _form_state('bc','be|br'); C_clen.focus }
  button vytvor_ico [210,14,,] { tag:'be', title:'Nový kontakt/IČO', skill:'hdce|hdce'
    help:'naplní položky základními hodnotami, vlastní vytvoření se provede uložením změn'
    proc onclick () { clear; _clen_ico } }
  proc _clen_ico () { var ico:number, ret:object
    clear; C_clen.init(1);
    ico.set(prompt2("Zadejte IČO firmy",'')); echo(ico);
    ret.set(ask('klub_firma_ico',ico));
    { ret.ok; copy_by_name(ret,C_clen);
      C_clen.prijmeni.change;
      C_clen.rodcis.change;
      C_clen.ulice.change;
      C_clen.obec.change;
      C_clen.psc.change;
    | alert(ret.err)
    }
#     _form_state('bc','be|br'); C_clen.focus
  }
  # Smazat
  button zrus_clena [336,14,,] { tag:'br', skill:'hdcd|hdcd', title:'Smazat',
    help:'smaže kontakt po předchozím dotazu (je vhodné napsat předtím do historie důvod)'
    proc onclick () {
      confirm('opravdu smazat kontakt č.',C_clen.id.get,' ',
        C_clen.jmeno.get,' ',C_clen.prijmeni.get,'?');
      C_clen.del.set(conc('D ',sys('user','abbr'),' ',now)); C_clen.del.change;
      _clen_uloz(1)
    | echo('kontakt zůstává jak byl') } }
  # Obnovit
  button obnov_clena [332,14,,] { tag:'br', skill:'hdcd|hdcd', title:'Obnovit',
    help:'obnoví kontakt po předchozím dotazu (je vhodné uvést důvod) '
    proc onclick () {
      confirm('opravdu obnovit kontakt č.',C_clen.id.get,' ',
        C_clen.jmeno.get,' ',C_clen.prijmeni.get,'?');
      C_clen.del.set(' '); C_clen.del.change;
      _clen_uloz(0)
    | echo('kontakt zůstává jak byl') } }
  # procedury
  proc _clen_load (id) {
    C_clen.load(id); _form_state('br|be','bc'); }
}
# ================================================================================================== _dary
form _dary [,,400,256] {
  label [9,6,50,15] { title:'<h1>Dary</h1>'}
  label [2,2,401,30] { css:'ae_parm' }
  label [104,4,20,] { title:'celkem' }
  label suma [95,16,50,17] { format:'r' }
  # přepínání sloupců
  check show_del [200,0,100,18] { title:'i smazané', format:'t', value:'0'
    proc onchange () {
      {  show_del.get; dary.del.width(10); dary.ucet.width(168)
      | dary.del.width(0); dary.ucet.width(179) };
      _dary_load
  } },
  check show_new [200,14,200,18] { title:'jen letošní a loňské', format:'t', value:'1'
    proc onchange () { _dary_load } }
  // -- potlačeno 13/8/2013
  // check show_our [327,0,72,17] { title:'jen naše', format:"t", help:'jen dary našemu středisku',
  //   value:'0', skill:'hdks|hdks' // tento skill je užit i v _dar_load
  //   proc onchange () { _dary_load } },
  # seznam darů
  view dd: table dar
  view dv: table _cis {join_type:"LEFT", join:"ON dd.varsym=dv.data AND dv.druh='varsym'" }
  browse dary [4,34,0,54] { rows:10, wheel:5, css_rows:'color,0:dar_2'
    show del    [,,0,]  { title:'D', data:dd.deleted, format:'q' },
    show nucet  [,,0,]  { title:'ú', data:dd.nas_ucet, map_pipe:cis_k_ucet.zkratka },
    show dne    [,,70,] { title:'ze dne', data:dd.castka_kdy, format:'rfs-' },
    show castka [,,60,] { title:'hodnota', data:dd.castka, format:'rfs' },
    show zpusob [,,12,] { title:'z', data:dd.zpusob, map_pipe:cis_k_zpusob.zkratka },
    show        [,,20,] { title:'s', data:dd.stredisko, map_pipe:cis_stredisko.zkratka },
    show        [,,32,] { title:'VS', data:dd.varsym },
    show ucet   [,,173,] { title:'-/účet/balíček/věc', expr:"if(zpusob=4,dd.popis,dd.ucet)",
                           format:'tfs' },
    show iddar  { data:dd.id_dar },
    show kdo    { data:dd.zmena_kdo, map_pipe:user.abbr },
    show kdn    { data:dd.zmena_kdo },
    show kdy    { data:dd.zmena_kdy }
    show color  { data:dv.zkratka },
    proc onrowclick() { dary_znacka; }
    proc onchange()   { dary_znacka; }
    proc dary_znacka () { dary.browse_active; C_dar._dar_load(iddar.get); set_zmena }
  }
  label zmena [12,238,391,18] { title:'' },
  # procedury
  proc set_zmena() {
    zmena.set(conc('<small>poslední změna: ',dary.kdn.get,' ',
      dary.kdy.get,'</small>')) }
  //n var nase: text -- potlačeno 13/8/2013
  proc _dary_load () {
    C_dar._dar_init;
    suma.set('');
    C_cleni.cleni.browse_key;
    //n nase.set(conc(" AND FIND_IN_SET(dd.stredisko,'",klu.cond_strediska.get,"')"));
    //n [ if(has_skill('hdks'),{nase.set(cconc(show_our.get,nase.get,''))}) ];
    dary.browse_load(conc(
      cconc(C_dary.show_del.get,'',"left(deleted,1)!='D' AND"),
      " id_clen=",C_cleni.cleni.browse_key,
      //n nase.get,
      cconc(C_dary.show_new.get," AND year(castka_kdy)>=year(curdate())-1")
    ));
    _dary_suma;
    dary.dary_znacka;
  }
  proc _dary_suma () {
    suma.set(ask('klub_dary_suma',C_cleni.cleni.browse_key,'')) } //n nase.get*/)) }
  proc _dary_focus () {
    dary.browse_focus }
}
# --------------------------------------------------------------------------------------------- _dar
use C_dar: form klu._dar [417,klu_cle_dary,,] {  skill:'hdd' // doplnění funkcionality metod klu._dar
  # Nový
  proc vytvor_dar.onclick() {
    form.desc._dar_novy(C_cleni.cleni.browse_key); }
  # Uložit
  func _dar_uloz_2 () { var stav: number
//    stav= _dar_uloz();
    echo('stav=',stav);
    //_dar_uloz: 0 - beze změny, 1 - uložení změny, 2 - vytvoření nového
    // s případnou úpravou příslušnosti ke středisku
    switch (stav) {
    case 1:
      C_dary.dary.browse_row(); C_dary._dary_suma();
      php.klub_clen_stredisko(C_dar.id_clen); C_clen.load(); C_cleni.cleni.browse_seek();
      break;
    case 2:
      if (C_dary.dary.browse_seek(conc('id_dar=',C_dar.key))) {
        C_dary._dary_suma();
        php.klub_clen_stredisko(C_dar.id_clen); C_clen.load(); C_cleni.cleni.browse_seek();
      }
      else {
        warning("dar byl vytvořen ale není zobrazený v levém seznamu (nevyhovuje nastaveným podmínkám)")
      }
    }
  }
//  proc _dar_uloz () {
//    var stav: number
//    stav.set(form.desc._dar_uloz);
//    echo('stav=',stav);
//    //_dar_uloz: 0 - beze změny, 1 - uložení změny, 2 - vytvoření nového
//    // s případnou úpravou příslušnosti ke středisku
//    { eq(stav,1);
//      C_dary.dary.browse_row; C_dary._dary_suma;
//      [ ask('klub_clen_stredisko',C_dar.id_clen.get); C_clen.load; C_cleni.cleni.browse_seek ]
//    | eq(stav,2);
//      { C_dary.dary.browse_seek(conc('id_dar=',C_dar.key)); C_dary._dary_suma;
//        [ ask('klub_clen_stredisko',C_dar.id_clen.get); C_clen.load; C_cleni.cleni.browse_seek ]
//      | warning("dar byl vytvořen ale není zobrazený v levém seznamu (nevyhovuje nastaveným podmínkám)")
//      }
//    }
//  }
  # Zpět
  proc _dar_zpet (iddar) { echo(3);
    form.desc._dar_zpet(C_dary.dary.browse_key); }
//   proc _dopis_uloz (typ) {  echo('a');
//     form.desc._dopis_uloz(typ);
//     echo('b');
//     C_dary.dary.browse_row;
//     echo('c');
//     C_dary.set_zmena }
}
