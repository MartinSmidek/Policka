# Systém CK - modul Dopisy
# (c) 2010-2015 Martin Šmídek <martin@smidek.eu>

var dopis_id: text
proc onstart () { dopis_id.set(0) }
# ================================================================================> DOPISY A ŠABLONY
# editace vzorů zasílaných dopisů a jejich hlaviček
panel dop {type:'right', title:"[fa-paperclip] Vzory/šablony", _sys:'*'
  use ii: form right [12,4,,]
  var cast_sablony: text
  var cast_dopis: text
  menu {type:'left', format:'f+'
    menu {title:'Vzory dopisů', type:'group'
      item {title:'Potvrzení finančního daru'   ,par:°{s:'texty',c:'Pf'} }
      item {title:' ... hromadné za období'     ,par:°{s:'texty',c:'Pfo'} }
      item {title:' ... hromadné za rok'        ,par:°{s:'texty',c:'Pfr'} }
      item {title:'Potvrzení věcného daru'      ,par:°{s:'texty',c:'Pv'} }
//      item {title:'Žádost o věcný dar'          ,par:°{s:'texty',c:'Zv'} }
//      item {title:'Vzor darovací smlouvy'       ,par:°{s:'texty',c:'Sv'} }
      proc onclick (i) {
        ii.opravit.display(1); S_sablona.hide;
        cast_dopis.set(ask('dop_sab_text',i.par.c));
        dopis_id.set(cast_dopis.get('id_dopis'));
        ii.nadpis(i.title,cast_dopis.get('obsah'))
      }
    }
    menu {title:'Vzory mailů', type:'group'
      item {title:'Rozesílání Zpravodajů'        ,par:°{typ:'bulletiny'} }
      proc onclick (i) {
        ii.opravit.display(1); S_sablona.hide;
        cast_dopis.set(ask('dop_sab_mail',i.par.typ));
        dopis_id.set(cast_dopis.get('id_dopis'));
        ii.nadpis(i.title,cast_dopis.get('obsah'))
      }
    }
    menu {title:'Části šablony dopisů', type:'group', _sys:'sab'
#       item {title:'hlavička - logo'             ,par:°{s:'D',c:'logo'} }
#       item {title:'hlavička - motto'            ,par:°{s:'D',c:'motto'} }
#       item {title:'kontakt'                     ,par:°{s:'D',c:'kontakt'} }
      item {title:'vyřizuje'                    ,par:°{s:'D',c:'vyrizuje'} }
      item {title:'telefon'                     ,par:°{s:'D',c:'telefon'} }
      item {title:'datum'                       ,par:°{s:'D',c:'odeslano'} }
      item {title:'věc'                         ,par:°{s:'D',c:'hlavicka'} }
#       item {title:'adresa'                      ,par:°{s:'D',c:'ramecek'} }
      item {title:'adresa'                      ,par:°{s:'D',c:'adresa'} }
      item {title:'adresa - IČ'                 ,par:°{s:'D',c:'ico'} }
#       item {title:'text - členské číslo'        ,par:°{s:'D',c:'clen'} }
#     //item {title:'text - přílohy'              ,par:°{s:'D',c:'plus'} }
      item {title:'text - dopis D'              ,par:°{s:'D',c:'text'} }
#       item {title:'patička - spojení'           ,par:°{s:'D',c:'paticka'} }
      proc onclick (i) {
        ii.opravit.display(0);
        cast_sablony.set(ask('dop_sab_cast',i.par.s,i.par.c));
        ii.nadpis(i.title,' ');
        S_sablona.popup(0,90);
      }
    }
    menu {title:'Ukázkové tisky šablon', type:'group', _sys:'sab'
      item {title:'šablona Charita'              ,par:°{s:'nahled',c:'nahled_j'} }
      proc onclick (i) {
        ii.opravit.display(0); S_sablona.hide;
        ii.nadpis(i.title,ask('dop_sab_nahled',i.par.c))
      }
    }
  }
  # --------------------------------------------------------------------------------==> . right
  # formulář pro levostranné menu s tlačítkem OPRAVIT a spinerem
  form right [,,*,600] {
    button opravit [-24,4,,] { title:'Upravit', style:'display:none;zIndex:2'
      proc onclick() { D_dopis.Edit(dopis_id.get); ii.nadpis('',D_dopis.d.html.get) }
    }
    label head [0,0,*,50] { title:'' }
    label note [0,50,*,500] { title:'' }
    proc nadpis(h,n) {
      [ h; head.set(conc("<div class='karta'>",h,"</div>")) ];
      [ n; note.set(n) ]
    }
    proc append(n) {
      note.set(conc(note.get,n))
    }
    label working [360,140,64,64] { title:"<img src='ch/img/spinner.gif'>",
      style:'z-index:999', format:'n' }
    proc work(on) { working.display(on) }
  }
  # -----------------------------------------------------------------------------------==> S_sablona
  # popup
  panel S_sablona [0,0,645,520] { title:' Úprava šablony dopisu',
    use cast: form _cast [0,0,,],
    proc onfocus () {
      cast.load(cast_sablony.get('id_dopis_cast'));
      cast.ukazka.set(cast_sablony.get('obsah'))
    }
    form _cast [,,700,447] {
      label [55,4,100,17] { title:'zleva:' },
      field l [94,1,33,17] { data:dopis_cast.l, format:'r' },
      label [144,5,50,17] { title:'zhora:' },
      field t [183,1,35,17] { data:dopis_cast.t, format:'r' },
      label [55,29,100,17] { title:'šířka:' },
      field w [96,28,33,17] { data:dopis_cast.w, format:'r' },
      label [142,30,50,17] { title:'výška:' },
      field h [183,28,35,17] { data:dopis_cast.h, format:'r' },
      label [267,32,50,17] { title:'zarovnání:' },
      field align [332,31,30,17] { data:dopis_cast.fattr },
      label [239,4,100,17] { title:'velikost písma:' },
      field fsize [332,3,31,17] { data:dopis_cast.fsize, format:'r' },
      label [381,6,50,17] { title:'řádkování:' },
      field ln [444,3,32,17] { data:dopis_cast.ln },
      label [376,31,100,17] { title:'ohraničení:' },
      field bord [444,29,33,17] { data:dopis_cast.bord, format:'' },
      label [490,6,100,17] { title:'umístění:' },
      field umisteni [550,2,16,17] { data:dopis_cast.umisteni, format:'' },
      label [518,28,100,17] { title:'typ:' },
      field typ [550,29,17,17] { data:dopis_cast.typ, format:'' },
      label [23,59,100,17] { title:'text (html):' },
      edit obsah [95,63,547,200] { data:dopis_cast.obsah },
      label [23,275,640,17] { title:"<u>orientační</u> ukázka (přesná viz menu 'Ukázkové tisky šablon'):" },
      label ukazka [23,295,640,500]
      button uloz [590,0,,] { title:'Uložit'
        proc onclick () { form.save; form.load }
      }
    }
  },
}
# =======================================================================================> D_dopis
# oprava textu, stavu v popup menu
panel D_dopis [0,0,645,520] { title:' Úprava textu dopisu', type:'popup', css:'dialog', skill:'hke'
  var dopis_id: number
  use d: form _dopis [0,0,,],
  proc Edit(id_dopis) { d.load(id_dopis); panel.modal(50,50);
#   proc onfocus () {
#     dopis_id.get; d.load(dopis_id.get);
#   | warning('nedefinovaný dopis')
  }
  # ------------------------------------------------------------------------------ _dopis
  form _dopis [10,10,600,460] {
    label [0,12,60,20] { title:'Aktuální:' }
    check nazev [50,10,50,20] { data:dopis.aktualni },
    button  [540,9,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }}
    button  [600,9,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    edit html [0,40,655,480] {type:'html', data:dopis.obsah, par:°{toolbar:'EzerLetter'} },
  }
}
# =======================================================================================> POTVRZENÍ
# rozesílání dávek potvrzení II
//panel pot {type:'right', title:"Potvrzení", _sys:'*', include:'onclick', skill:'hkp' }
# =========================================================================================> EXPORTY
# exporty adres
//panel sql {type:'right', title:"Exporty", _sys:'*', include:'onclick', skill:'hks' }
# ======================================================================================> PODĚKOVÁNÍ
# rozesílání dávek poděkování dle VS
# panel pot {type:'right', title:"Poděkování", _sys:'*', include:'onclick', skill:'hkp' }
# =========================================================================================> TABULKY

table davka { key_id:'id_davka'
  number id_davka { key:'primary' },
  text   stav
  text   druh	                  // o=obdobi, r=rok, p1,p2=pololeti
  text   pro                      // ''=potvrzení a bulletin, 'F'=firmy
  text   nazev                    // automaticky tvořený název
  number id_dopis { help:'potvrzeni|dopis použitý jako potvrzující' },
  date   datum_od { sql_pipe:'sql_date1' }
  date   datum_nw { sql_pipe:'sql_date1' }      // datum pro firmy uvozující "čerstvé dary"
  date   datum_do { sql_pipe:'sql_date1' }
  number let_zpet                 // poslat bulletin starým dárcům
  number mailem                   // použít i mailovou rozesílku bulletinu
//  number vanocni                  // varianta pro rozesílku vánočního bulletinu
  text   resume
}

table dopis { key_id:'id_dopis'
  number id_dopis { key:'primary' },
  number id_davka { help:'dávka|číslo rozesílání dávky dopisů' },
  text nazev
  text odesilatel
  text prilohy
  text druh { help:'druh|D-dopis, S-samolepka,legitimace, N-nesamostatná složka' },
  text typ { help:'typ|značka dopisu' },
  number nw
  number nh
  number komu     // pouze pro maily
  number pocet    // pouze pro maily
  text report { help:'vzor|identifikátor reportu' },
  number aktualni { help:'aktualni|text dopisu je připraven k tisku' },
  date datum { help:'datum|vročení dopisu', sql_pipe:'sql_date1' },
  text obsah { help:'obsah|text dopisu' },
  text var_list { help:'seznam proměnných'}
  text post_vars { help:'seznam proměnných počítaných po generování'}
  text nest_list { help:'seznam složek (druh=N)'}
  text add_list
}

table dopis_cast { key_id:'id_dopis_cast'
  number id_dopis_cast { key:'primary' },
  text name
  text umisteni
  text typ
  number l
  number t
  number w
  number h
  number ln
  text align
  number fsize
  text fattr
  text bord
  text obsah
}

table mail { key_id:'id_mail'
  number id_mail { key:'primary' },
  number id_dopis { help:'mail|index mailu' }, // nepoužívá se pro dopisy s druh!='@'
  number id_davka
  number id_znacka                             // nepoužívá se pro dopisy s druh=='@'
  number id_clen
  text znacka
  text email
  number stav
  text msg
  text body
  text prilohy
}

# ============================================================================================> MAPY

map map_k_zpusob: table _cis { where:"druh='k_zpusob'", order:'poradi', key_id:'data'}

map map_k_ucet:   table _cis { where:"druh='b_ucty'", order:'poradi', key_id:'data'}

map map_dr_typ:   table _cis { where:"druh='dr_typ'", order:'poradi', key_id:'data' }

